plugins {
  id 'java'
  id 'application'
  id "edu.sc.seis.macAppBundle" version "2.3.0"
  id 'org.openjfx.javafxplugin' version '0.0.7'
}

javafx {
  modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.swing']
  version = "12-ea+8"
}

dependencies {
  compile 'com.github.tulskiy:jkeymaster:1.2'
  compile 'com.calendarfx:view:11.5.0'
  
  
  runtime 'de.jensd:fontawesomefx-fontawesome:4.6.1-2'
  runtime 'org.controlsfx:controlsfx:11.0.0'

  runtime 'org.openjfx:javafx-controls:12-ea+8'
  runtime 'org.openjfx:javafx-graphics:12-ea+8'
  runtime 'org.openjfx:javafx-swing:12-ea+8'

  compile project (':spica-javaclient')
  compile project (':spica-commons')
}



application {
  mainClassName = "org.spica.devclient.DevApplication"
}

macAppBundle {
  mainClassName = 'org.spica.devclient.DevApplication'
  appName = 'Spica'
  dmgName = "Spica-${project.version}"
}

boolean isMac = System.getProperty("os.name").toLowerCase().contains("mac")
println "Is Mac: " + isMac

tasks.build.dependsOn tasks.createDmg
tasks.createDmg.onlyIf {isMac}

task installDmg(type: Copy) {
  from "build/macApp/${macAppBundle.appName}.app"
  into "/Applications/${macAppBundle.appName}.app"
  dependsOn tasks.createDmg
  onlyIf {isMac}
  //eachFile { println "Copying " + it.name}
}

task createDemoData (type: JavaExec) {
  classpath = sourceSets.test.runtimeClasspath
  main = 'org.spica.javaclient.model.DevClientDemoDataCreator'
}

def localCliInstallationPath = System.getProperty("user.home") + '/programs/spica-devclient'


task clearLocalDevClientInstallationPath(type: Delete) {
  delete localCliInstallationPath
  followSymlinks = true
}


task installLocal(type: Copy) {
  def zipFile = project(':spica-devclient').file("build/distributions/spica-devclient-${version}.zip")
  println zipFile.absolutePath
  dependsOn 'clearLocalDevClientInstallationPath'
  from zipTree(zipFile)
  eachFile {
    path = path.substring(path.indexOf("/"), path.length())
  }
  into localCliInstallationPath
  includeEmptyDirs = false
  dependsOn tasks.distZip
}
